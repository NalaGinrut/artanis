;;  -*-  indent-tabs-mode:nil; coding: utf-8 -*-
;;  === Ssql tests ===
;;  Copyright (C) 2015,2019,2021
;;      "Mu Lei" known as "NalaGinrut" <NalaGinrut@gmail.com>
;;  Artanis is free software: you can redistribute it and/or modify
;;  it under the terms of the GNU General Public License as published by
;;  the Free Software Foundation, either version 3 of the License, or
;;  (at your option) any later version.

;;  Artanis is distributed in the hope that it will be useful,
;;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;  GNU General Public License for more details.

;;  You should have received a copy of the GNU General Public License
;;  along with this program.  If not, see <http://www.gnu.org/licenses/>.

(define-module (test ssql)
  #:use-module (artanis utils)
  #:use-module (artanis ssql)
  #:use-module (test-suite lib))

(with-test-prefix "SSQL test"
  (pass-if "SELECT test1"
    (string=? (->sql select * from 'Persons)
              "select * from Persons;"))

  (pass-if "SELECT test2"
    (string=? (->sql select '(name age) from 'Persons)
              "select name,age from Persons;"))

  (pass-if "SELECT test3"
    (let ((columns (map (lambda (c) (format #f "lcase(~a)" c)) '(name city))))
      (string=? (->sql select columns from 'Persons)
                "select lcase(name),lcase(city) from Persons;")))

  (pass-if "SELECT & WHERE test1"
    (string=? (->sql select * from 'Persons (where (/and #:age> 15 #:age< 30)))
              "select * from Persons where age>15 and age<30;"))

  (pass-if "SELECT & WHERE test2"
    (string=? (->sql select * from 'Person (where (/or #:name "John" #:age 15 (/and #:c 2 #:d 4))))
              "select * from Person where name='John' or age=15 or c=2 and d=4;"))

  (pass-if "SELECT & WHERE test3"
    (string=? (->sql select * from 'Person (where (/or #:name "John" #:age 15 #:email "john@artanis.com" (/and #:c 2 #:d 4) )))
              "select * from Person where name='John' or age=15 or email='john@artanis.com' or c=2 and d=4;"))

  (pass-if "WHERE group test1"
    (string=? (where #:age<> '(1 2))
              " where age<>1 and age<>2"))

  ;; The literal string must use (/literal str) to avoid quotes dropping.
  (pass-if "SELECT literals test"
    (string=? (->sql select (list (/literal "John") 25 (/literal "New York"))
                     from 'Persons)
              "select 'John',25,'New York' from Persons;"))

  ;; Test literals and function calling
  (pass-if "SELECT literals & functions test"
    (string=? (->sql select (list (/literal "John")
                                  "lcase(city)"
                                  25)
                     from 'Persons)
              "select 'John',lcase(city),25 from Persons;"))

  (pass-if "SELECT single literal test1"
    (string=? (->sql select (/literal "Hello, World!")
                     from 'Dual)
              "select 'Hello, World!' from Dual;"))

  (pass-if "Test single literal: SELECT 1 from Table"
    (string=? (->sql select 1 from 'Table)
              "select 1 from Table;"))

  (pass-if "Test single literal: SELECT * from Table"
    (string=? (->sql select * from 'Table)
              "select * from Table;"))

  (pass-if "WHERE EXISTS test1"
    (string=? (->sql select * from 'Employees
                     (where (/exists (->sql select 1 from 'Managers
                                            (where #:id 123)))))
              "select * from Employees where exists (select 1 from Managers where id=123);"))
  )
